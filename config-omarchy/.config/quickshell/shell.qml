//@ pragma UseQApplication
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import QtQuick
import QtQuick.Layouts
import "bar"
import "bar/modules"
import "dock"
import "settings"
import "launcher"
import "widgets"
import "taskmanager"

ShellRoot {
    id: shell

    readonly property string homeDir: Quickshell.env("HOME") || ""
    readonly property string omarchyCurrentDir: homeDir + "/.config/omarchy/current"
    readonly property string omarchyThemeNamePath: omarchyCurrentDir + "/theme.name"
    readonly property string omarchyThemeColorsPath: omarchyCurrentDir + "/theme/colors.toml"
    readonly property real uiScale: Math.min(2.5, envScale("QS_UI_SCALE", 0.0))
    readonly property real uiScaleMultiplier: Math.max(0.25, Math.min(2.5, envScale("QS_UI_SCALE_MULTIPLIER", 0.6)))
    readonly property real popupScale: Math.max(1.0, Math.min(2.5, envScale("QS_POPUP_SCALE", uiScale > 0 ? uiScale : 1.0)))
    property bool overviewActive: false

    property string bg:        "#1e1e2e"
    property string fg:        "#cdd6f4"
    property string accent:    "#89b4fa"
    property string dim:       "#45475a"
    property string highlight: "#cba6f7"
    property string red:       "#f38ba8"
    property string green:     "#a6e3a1"
    property string muted:     "#585b70"
    readonly property var palette: ({
        bg:        shell.bg,
        fg:        shell.fg,
        accent:    shell.accent,
        dim:       shell.dim,
        muted:     shell.muted,
        highlight: shell.highlight,
        red:       shell.red,
        green:     shell.green
    })

    SettingsState {
        id: settingsState
    }

    function envScale(name, fallback) {
        const value = Number(Quickshell.env(name) || fallback)
        return isNaN(value) ? fallback : value
    }

    QtObject {
        id: powerActions

        property bool open: false
        property string title: ""
        property string message: ""
        property var command: null
        property int selectedIndex: 0

        function requestAction(titleText, messageText, cmd) {
            title = titleText
            message = messageText
            command = cmd
            selectedIndex = 0
            open = true
        }

        function close() {
            open = false
            command = null
            selectedIndex = 0
        }

        function moveSelection(delta) {
            selectedIndex = (selectedIndex + delta + 2) % 2
        }

        function activateSelected() {
            if (selectedIndex === 0) close()
            else confirm()
        }

        function confirm() {
            if (!command) {
                close()
                return
            }

            if (Array.isArray(command))
                Quickshell.execDetached(command)
            else
                Quickshell.execDetached(["bash", "-lc", command])

            close()
        }
    }

    function parseToml(raw) {
        function get(key) {
            const rx = new RegExp('(?:^|\\n)' + key + '\\s*=\\s*"(#[0-9a-fA-F]{3,8})"')
            const m  = raw.match(rx)
            return m ? m[1] : null
        }
        bg        = get("background") || bg
        fg        = get("foreground") || fg
        accent    = get("accent")     || accent
        dim       = get("color0")     || dim
        muted     = get("color8")     || muted
        highlight = get("color5")     || highlight
        red       = get("color1")     || red
        green     = get("color2")     || green
    }

    Process {
        id: themeLoader
        command: ["bash", "-lc", "cat " + shell.omarchyThemeColorsPath + " 2>/dev/null"]
        running: true
        stdout: SplitParser {
            property string buf: ""
            onRead: data => buf += data + "\n"
        }
        onExited: {
            if (themeLoader.stdout.buf.length > 10)
                parseToml(themeLoader.stdout.buf)
            themeLoader.stdout.buf = ""
        }
    }

    Process {
        id: themeWatcher
        command: ["bash", "-lc",
            "if command -v inotifywait >/dev/null 2>&1; then " +
            "  exec inotifywait -m -e close_write " + shell.omarchyThemeNamePath + "; " +
            "else " +
            "  last=''; " +
            "  while true; do " +
            "    cur=$(stat -c %Y " + shell.omarchyThemeNamePath + " 2>/dev/null || echo missing); " +
            "    if [ \"$cur\" != \"$last\" ]; then printf 'changed\\n'; last=\"$cur\"; fi; " +
            "    sleep 3; " +
            "  done; " +
            "fi"
        ]
        running: true
        stdout: SplitParser {
            onRead: _ => {
                themeLoader.stdout.buf = ""
                themeLoader.running = false
                themeLoader.running = true
            }
        }
    }

    Process {
        id: submapProbe
        command: ["bash", "-lc", "hyprctl submap 2>/dev/null"]
        running: true
        stdout: SplitParser {
            property string buf: ""
            onRead: data => buf += data
        }
        onExited: {
            const current = (submapProbe.stdout.buf || "").trim()
            shell.overviewActive = current === "hyprtasking"
            submapProbe.stdout.buf = ""
        }
    }

    Timer {
        interval: shell.overviewActive ? 250 : 500
        repeat: true
        running: true
        onTriggered: {
            if (!submapProbe.running)
                submapProbe.running = true
        }
    }

    Bar {
        id: bar
        launcher:  appLauncher
        notifServer: notifServer
        powerActions: powerActions
        settings: settingsState
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
        bg:        shell.bg
        fg:        shell.fg
        accent:    shell.accent
        dim:       shell.dim
        highlight: shell.highlight
        red:       shell.red
        green:     shell.green
        muted:     shell.muted
        quietMode: shell.overviewActive
    }

    Dock {
        theme: shell.palette
        settings: settingsState
        launcher: appLauncher
        notifServer: notifServer
        quietMode: shell.overviewActive
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    ControlCenter {
        id: controlCenter
        theme: shell.palette
        notifServer: notifServer
        powerActions: powerActions
        settingsWindow: settingsWindow
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    Launcher {
        id: appLauncher
        theme: shell.palette
        powerActions: powerActions
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    ThemePicker {
        id: themePicker
        theme: shell.palette
        uiScale: shell.popupScale
    }

    BgPicker {
        id: bgPicker
        theme: shell.palette
        uiScale: shell.popupScale
    }

    KeybindViewer {
        id: keybindViewer
        theme: shell.palette
        uiScale: shell.popupScale
    }

    Clipboard {
        id: clipboardManager
        theme: shell.palette
        uiScale: shell.popupScale
    }

    EmojiPicker {
        id: emojiPicker
        theme: shell.palette
        uiScale: shell.popupScale
    }

    TaskManager {
        id: taskManager
        theme: shell.palette
        uiScale: shell.popupScale
    }

    DynamicClock {
        id: desktopClock
        theme: shell.palette
        settings: settingsState
        quietMode: shell.overviewActive
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    CalendarWidget {
        id: desktopCalendar
        theme: shell.palette
        settings: settingsState
        quietMode: shell.overviewActive
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    PomodoroWidget {
        id: pomodoroWidget
        theme: shell.palette
        settings: settingsState
        quietMode: shell.overviewActive
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    TodoWidget {
        id: todoWidget
        theme: shell.palette
        settings: settingsState
        quietMode: shell.overviewActive
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    SettingsWindow {
        id: settingsWindow
        theme: shell.palette
        state: settingsState
        uiScale: shell.popupScale
    }
    
    NotificationPanel {
        theme: shell.palette
        settings: settingsState
        uiScale: shell.uiScale
        uiScaleMultiplier: shell.uiScaleMultiplier
    }

    OsdService {
        id: osdService
        quietMode: shell.overviewActive
    }

    Osd {
        service: osdService
        settings: settingsState
        theme: shell.palette
        uiScale: shell.popupScale
    }

    NotificationServer {
        id: notifServer
    }

    Process {
        id: googleCalendarWatcher
        command: [
            "bash",
            "-lc",
            "if command -v nika-google-calendar >/dev/null 2>&1; then exec nika-google-calendar watch --thresholds 10,5,1; else exec sleep infinity; fi"
        ]
        running: true
    }

    IpcHandler {
        target: "openSettings"
        function handle() {
            settingsWindow.showing = true
        }
    }

    PanelWindow {
        id: powerConfirm
        visible: powerActions.open
        color: "transparent"
        anchors { top: true; left: true; right: true; bottom: true }
        exclusiveZone: 0
        WlrLayershell.layer: WlrLayer.Overlay
        WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand

        onVisibleChanged: {
            if (visible)
                powerConfirmKeyScope.forceActiveFocus()
        }

        FocusScope {
            id: powerConfirmKeyScope
            anchors.fill: parent
            focus: powerActions.open

            Keys.onPressed: event => {
                if (!powerActions.open) return

                if (event.key === Qt.Key_Left || event.key === Qt.Key_Up || event.key === Qt.Key_Backtab) {
                    powerActions.moveSelection(-1)
                    event.accepted = true
                } else if (event.key === Qt.Key_Right || event.key === Qt.Key_Down || event.key === Qt.Key_Tab) {
                    powerActions.moveSelection(1)
                    event.accepted = true
                } else if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter || event.key === Qt.Key_Space) {
                    powerActions.activateSelected()
                    event.accepted = true
                } else if (event.key === Qt.Key_Escape) {
                    powerActions.close()
                    event.accepted = true
                }
            }

            MouseArea {
                anchors.fill: parent
                onClicked: powerActions.close()

                Rectangle {
                    id: powerConfirmCard
                    width: 320
                    implicitHeight: contentColumn.implicitHeight + 32
                    transformOrigin: Item.Center
                    radius: 14
                    color: shell.bg
                    border.color: shell.dim
                    border.width: 1
                    anchors.centerIn: parent
                    opacity: powerActions.open ? 1 : 0
                    scale: shell.popupScale * (powerActions.open ? 1 : 0.96)

                    Behavior on opacity {
                        NumberAnimation { duration: 140; easing.type: Easing.OutCubic }
                    }

                    Behavior on scale {
                        NumberAnimation { duration: 170; easing.type: Easing.OutCubic }
                    }

                    MouseArea {
                        anchors.fill: parent
                        onClicked: {}
                    }

                    ColumnLayout {
                        id: contentColumn
                        anchors.fill: parent
                        anchors.margins: 16
                        spacing: 12

                        Text {
                            text: powerActions.title
                            color: shell.fg
                            font.pixelSize: 14
                            font.family: "JetBrainsMono Nerd Font Propo"
                            font.weight: Font.DemiBold
                        }

                        Text {
                            text: powerActions.message
                            color: shell.muted
                            font.pixelSize: 10
                            font.family: "JetBrainsMono Nerd Font Propo"
                            wrapMode: Text.WordWrap
                            Layout.fillWidth: true
                        }

                        RowLayout {
                            Layout.fillWidth: true
                            spacing: 8

                            Rectangle {
                                id: cancelButton
                                Layout.fillWidth: true
                                height: 32
                                radius: 9
                                color: powerActions.selectedIndex === 0
                                    ? Qt.alpha(shell.accent, 0.16)
                                    : Qt.alpha(shell.dim, 0.45)
                                border.color: powerActions.selectedIndex === 0
                                    ? Qt.alpha(shell.accent, 0.5)
                                    : Qt.alpha(shell.dim, 0.7)
                                border.width: 1
                                Behavior on color { ColorAnimation { duration: 120 } }
                                Behavior on border.color { ColorAnimation { duration: 120 } }

                                Text {
                                    anchors.centerIn: parent
                                    text: "Cancel"
                                    color: powerActions.selectedIndex === 0 ? shell.accent : shell.fg
                                    font.pixelSize: 10
                                    font.family: "JetBrainsMono Nerd Font Propo"
                                }

                                MouseArea {
                                    anchors.fill: parent
                                    cursorShape: Qt.PointingHandCursor
                                    hoverEnabled: true
                                    onEntered: powerActions.selectedIndex = 0
                                    onClicked: powerActions.close()
                                }
                            }

                            Rectangle {
                                id: confirmButton
                                readonly property bool armed: powerActions.selectedIndex === 1

                                Layout.fillWidth: true
                                height: 32
                                radius: 9
                                color: armed
                                    ? Qt.alpha(shell.red, 0.28)
                                    : Qt.alpha(shell.dim, 0.35)
                                border.color: armed
                                    ? Qt.alpha(shell.red, 0.72)
                                    : Qt.alpha(shell.dim, 0.55)
                                border.width: 1
                                Behavior on color { ColorAnimation { duration: 120 } }
                                Behavior on border.color { ColorAnimation { duration: 120 } }

                                Text {
                                    anchors.centerIn: parent
                                    text: "Confirm"
                                    color: confirmButton.armed ? shell.fg : shell.muted
                                    font.pixelSize: 10
                                    font.family: "JetBrainsMono Nerd Font Propo"
                                    font.weight: Font.DemiBold
                                }

                                MouseArea {
                                    anchors.fill: parent
                                    cursorShape: Qt.PointingHandCursor
                                    onClicked: {
                                        if (confirmButton.armed)
                                            powerActions.confirm()
                                        else
                                            powerActions.selectedIndex = 1
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    // ALL IpcHandler here for keybinds 
    IpcHandler {
        target: "openKeybindings"
        function handle() {
            keybindViewer.showing = true
        }
    }

    IpcHandler {
        target: "openMenu"
        function handle() {
            appLauncher.mode    = "menu"
            appLauncher.showing = true
        }
    }

    IpcHandler {
        target: "openApps"
        function handle() {
            appLauncher.mode          = "apps"
            appLauncher.appSearchText = ""
            appLauncher.showing       = true
        }
    }

    IpcHandler {
        target: "openThemes"
        function handle() {
            themePicker.showing = true
        }
    }

    IpcHandler {
        target: "openThemePicker"
        function handle() {
            themePicker.showing = true
        }
    }

    IpcHandler {
        target: "openBackgroundPicker"
        function handle() {
            bgPicker.showing = true
        }
    }

    IpcHandler {
        target: "openScreenrecord"
        function handle() {
            appLauncher.openScreenrecord()
        }
    }

    IpcHandler {
        target: "openSystem"
        function handle() {
            appLauncher.openSystem()
        }
    }

    IpcHandler {
        target: "openToggle"
        function handle() {
            appLauncher.openToggle()
        }
    }

    IpcHandler {
        target: "openClipboard"
        function handle() {
            clipboardManager.showing = true
        }
    }

    IpcHandler {
        target: "openEmojiPicker"
        function handle() {
            emojiPicker.showing = true
        }
    }

    IpcHandler {
        target: "openTaskManager"
        function handle() {
            taskManager.showing = true
        }
    }


    IpcHandler {
        target: "toggleDnd"
        function handle() {
            notifServer.toggleDnd()
        }
    }

    IpcHandler {
        target: "osdVolume"
        function handle() {
            osdService.showVolume()
        }
    }

    IpcHandler {
        target: "osdVolumeUp"
        function handle() {
            osdService.volumeStep(5)
        }
    }

    IpcHandler {
        target: "osdVolumeDown"
        function handle() {
            osdService.volumeStep(-5)
        }
    }

    IpcHandler {
        target: "osdVolumeMute"
        function handle() {
            osdService.toggleMute()
        }
    }

    IpcHandler {
        target: "osdBrightness"
        function handle() {
            osdService.showBrightness()
        }
    }

    IpcHandler {
        target: "osdBrightnessUp"
        function handle() {
            osdService.brightnessStep(5)
        }
    }

    IpcHandler {
        target: "osdBrightnessDown"
        function handle() {
            osdService.brightnessStep(-5)
        }
    }

    IpcHandler {
        target: "osdMic"
        function handle() {
            osdService.showMic()
        }
    }

    IpcHandler {
        target: "osdMedia"
        function handle() {
            osdService.showMediaStatus()
        }
    }

    IpcHandler {
        target: "osdMediaPlayPause"
        function handle() {
            osdService.mediaPlayPause()
        }
    }

    IpcHandler {
        target: "osdMediaNext"
        function handle() {
            osdService.mediaNext()
        }
    }

    IpcHandler {
        target: "osdMediaPrev"
        function handle() {
            osdService.mediaPrev()
        }
    }

    // Click Catcher Here 
    ClickCatcher {
        active: appLauncher.showing
            || themePicker.showing
            || bgPicker.showing
            || keybindViewer.showing
            || clipboardManager.showing
            || notifServer.panelOpen
            || controlCenter.showing
            || controlCenter.wifiManagerOpen
            || controlCenter.btManagerOpen
        topInset: controlCenter.showing && !bar.barOnBottom ? bar.exclusiveZone : 0
        bottomInset: controlCenter.showing && bar.barOnBottom ? bar.exclusiveZone : 0
        onClicked: {
            appLauncher.showing   = false
            themePicker.showing   = false
            bgPicker.showing      = false
            keybindViewer.showing = false
            clipboardManager.showing = false
            notifServer.panelOpen = false
            controlCenter.showing = false
            controlCenter.wifiManagerOpen = false
            controlCenter.btManagerOpen = false
        }
    }

}
